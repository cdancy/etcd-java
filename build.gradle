buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'checkstyle'

apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/documentation.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/release.gradle"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'io.atomix.copycat:copycat-server:1.1.4'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile group: 'io.atomix.catalyst', name: 'catalyst-local', version: '1.1.1'
    compile group: 'co.cask.http', name: 'netty-http', version: '0.16.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile group: 'io.protostuff', name: 'protostuff-core', version: '1.5.1'
    compile group: 'io.protostuff', name: 'protostuff-runtime', version: '1.5.1'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.1.0'
}

ext.compatibilityVersion = '1.8'
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

jar {
    manifest {
        attributes 'Implementation-Title': 'etcd-java',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}


checkstyle {
    toolVersion = "6.13"
}


tasks.withType(JavaCompile) {
    options.compilerArgs += ["-Xlint:-options"]
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }

}

task mockTest(type: Test) {
    useTestNG()
    include "**/*MockTest.class"
}

task integTest(type: Test, dependsOn: ['mockTest']) {
    useTestNG()
    include "**/**LiveTest.class"
    doFirst {
        String runtimeURL = bootstrapDocker ? "http://${getLocalHost()}:2379" : "${testEtcdEndpoint}"
        systemProperties = ["test.etcd.endpoint" : runtimeURL]
    }
}